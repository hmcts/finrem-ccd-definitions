plugins {
    id 'java'
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'com.github.hmcts'
version 'DEV-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// endregion

// region test coverage

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

task highLevelDataSetup(type: JavaExec) {
    dependsOn classes
    main = "finrem.HighLevelDataSetupApp"
    classpath +=  sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    testImplementation(platform(group: 'org.junit', name: 'junit-bom', version: '5.10.3'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.0'

    testImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.17.2'
    testImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.17.2'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.16.1'
    testImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.0.2', {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.21.22'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.13'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.13'

    testRuntimeOnly group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
    testImplementation group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
}

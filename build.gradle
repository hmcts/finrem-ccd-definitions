plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'pmd'
}

group 'com.github.hmcts'
version 'DEV-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations.all {
    exclude group: 'commons-logging', module: 'commons-logging'
}

// region static code analysis

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// endregion

// region test coverage

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

task highLevelDataSetup(type: JavaExec) {
    dependsOn classes
    main = "finrem.HighLevelDataSetupApp"
    classpath +=  sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    testImplementation(platform(group: 'org.junit', name: 'junit-bom', version: '5.11.0'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'

    testImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.1.0'
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.16.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.3'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'

    testRuntimeOnly group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
}
